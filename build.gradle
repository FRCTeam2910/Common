/*
 * The following block allows us to be used as a submodule. If we are the root
 * project we want to specify a version of GradleRIO to use, however, when we
 * are not the root project (i.e. a submodule in a robot) let the parent
 * project specify what version of GradleRIO to use by not adding GradleRIO to
 * the plugin classpath.
 */
buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        if (rootProject == project) {
            classpath 'edu.wpi.first:GradleRIO:2019.4.1'
        }
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = 'org.frcteam2910.common'
version = '2019.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
}

publishing {
    publications {
        common(MavenPublication) {
            from components.java
        }
    }
}

// Only add a wrapper block if we are the root project
if (rootProject == project) {
    wrapper {
        distributionType = Wrapper.DistributionType.ALL
        gradleVersion = '5.1'
    }
}
